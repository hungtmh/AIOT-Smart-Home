#include <WiFi.h>
#include <HTTPClient.h>
#include "esp_camera.h"

//================ WiFi ==================
// const char* WIFI_SSID = "Chicken";
// const char* WIFI_PASS = "abcxyz123";
const char* WIFI_SSID     = "Huy Duy";
const char* WIFI_PASS  = "Vietnam060512";
//======= Python Flask Server ============
const char* SERVER_URL = "http://192.168.1.3:5000/upload";

//============== Camera Pins (AI Thinker) ==============
#define PWDN_GPIO_NUM     32
#define RESET_GPIO_NUM    -1
#define XCLK_GPIO_NUM      0
#define SIOD_GPIO_NUM     26
#define SIOC_GPIO_NUM     27

#define Y9_GPIO_NUM       35
#define Y8_GPIO_NUM       34
#define Y7_GPIO_NUM       39
#define Y6_GPIO_NUM       36
#define Y5_GPIO_NUM       21
#define Y4_GPIO_NUM       19
#define Y3_GPIO_NUM       18
#define Y2_GPIO_NUM        5
#define VSYNC_GPIO_NUM    25
#define HREF_GPIO_NUM     23
#define PCLK_GPIO_NUM     22

unsigned long lastCapture = 0;

//======================================================
void uploadImage(camera_fb_t *fb)
{
    if (WiFi.status() != WL_CONNECTED)
    {
        Serial.println("WiFi disconnected!");
        return;
    }

    HTTPClient http;

    http.begin(SERVER_URL);
    http.addHeader("Content-Type", "image/jpeg");

    int httpCode = http.POST(fb->buf, fb->len);

    if (httpCode > 0)
    {
        Serial.printf("Upload success. HTTP Code: %d\n", httpCode);
    }
    else
    {
        Serial.printf("Upload failed: %s\n",
                      http.errorToString(httpCode).c_str());
    }

    http.end();
}

//======================================================
void setup()
{
    Serial.begin(115200);

    camera_config_t config;

    config.ledc_channel = LEDC_CHANNEL_0;
    config.ledc_timer = LEDC_TIMER_0;

    config.pin_d0 = Y2_GPIO_NUM;
    config.pin_d1 = Y3_GPIO_NUM;
    config.pin_d2 = Y4_GPIO_NUM;
    config.pin_d3 = Y5_GPIO_NUM;
    config.pin_d4 = Y6_GPIO_NUM;
    config.pin_d5 = Y7_GPIO_NUM;
    config.pin_d6 = Y8_GPIO_NUM;
    config.pin_d7 = Y9_GPIO_NUM;

    config.pin_xclk = XCLK_GPIO_NUM;
    config.pin_pclk = PCLK_GPIO_NUM;
    config.pin_vsync = VSYNC_GPIO_NUM;
    config.pin_href = HREF_GPIO_NUM;

    config.pin_sscb_sda = SIOD_GPIO_NUM;
    config.pin_sscb_scl = SIOC_GPIO_NUM;

    config.pin_pwdn = PWDN_GPIO_NUM;
    config.pin_reset = RESET_GPIO_NUM;

    config.xclk_freq_hz = 10000000;
    config.pixel_format = PIXFORMAT_JPEG;

    config.frame_size = FRAMESIZE_QVGA;
    config.jpeg_quality = 15;
    config.fb_count = 1;

    if (esp_camera_init(&config) != ESP_OK)
    {
        Serial.println("Camera Init Failed");
        while (true);
    }

    Serial.println("Connecting WiFi...");

    WiFi.begin(WIFI_SSID, WIFI_PASS);

    while (WiFi.status() != WL_CONNECTED)
    {
        delay(500);
        Serial.print(".");
    }

    Serial.println();
    Serial.println("================================");
    Serial.println("WiFi Connected");
    Serial.print("ESP32 IP: ");
    Serial.println(WiFi.localIP());

    Serial.print("Upload Server: ");
    Serial.println(SERVER_URL);
    Serial.println("================================");
}

//======================================================
void loop()
{
    if (millis() - lastCapture >= 2000)
    {
        lastCapture = millis();

        camera_fb_t *fb = esp_camera_fb_get();

        if (!fb)
        {
            Serial.println("Capture Failed");
            return;
        }

        Serial.printf("Captured image (%u bytes)\n", fb->len);

        uploadImage(fb);

        esp_camera_fb_return(fb);
    }
}